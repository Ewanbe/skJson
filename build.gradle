import java.security.MessageDigest

plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'cz.coffee'
version = '4.1.0'
sourceCompatibility = '21'
targetCompatibility = '21'

static def generateSHA1() {
    def time = System.currentTimeMillis().toString()
    def md = MessageDigest.getInstance("SHA-1")
    def digest = md.digest(time.bytes)
    return new BigInteger(1, digest).toString(16).padLeft(40, '0')
}

static def generateShortSHA1(sha1) {
    return sha1.substring(0, 8)
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://repo.skriptlang.org/releases" }
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    maven { url "https://repo.papermc.io/repository/maven-public/" }
    maven { url "https://repo.purpurmc.org/snapshots" }
}

apply from: 'dependencies.gradle'
apply from: 'resources.gradle'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def fullRev = generateSHA1()
def shortRev = generateShortSHA1(fullRev)

tasks.processResources {
    filesMatching("plugin.yml") {
        expand(
                "version": project.version,
                "rev": "" + shortRev
        )
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'SkJson',
                'Implementation-Version': version
        )
    }
}

task shadeJar(type: Jar) {
    archiveClassifier.set('shaded')
    from(sourceSets.main.output)

    // Exclude META-INF files
    exclude 'META-INF/*.MF', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'

    doLast {
        copy {
            from archiveFile.get().asFile
            into 'C:\\Users\\Coffee\\Desktop\\mc-developing\\plugins'
            rename { 'SkJson.jar' }
        }
    }
}

task withServer {
    doLast {
        copy {
            from tasks.named("shadeJar").get().archiveFile.get().asFile
            into 'C:\\Users\\Coffee\\Desktop\\mc-developing\\plugins'
            rename { 'SkJson.jar' }
        }
    }
    dependsOn 'clean'
    dependsOn 'shadeJar'
    finalizedBy 'runServer'
}

runServer {
    minecraftVersion("1.21.3")

    downloadPlugins {
        url('https://github.com/SkriptLang/Skript/releases/download/2.9.4/Skript-2.9.4.jar')
    }
}
